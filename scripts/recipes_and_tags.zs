#priority 100

import crafttweaker.api.recipe.Replacer;
import crafttweaker.api.item.IItemStack;
import crafttweaker.api.ingredient.IIngredient;

var air = <item:minecraft:air>;
var amethyst_block = <item:minecraft:amethyst_block>;
var ametrine_block = <item:byg:ametrine_block>;
var andesite = <item:minecraft:andesite>;
var ash_brick = <item:supplementaries:ash_brick>;
var ash_iw = <item:immersive_weathering:ash_layer_block>;
var ash_supplementaries = <item:supplementaries:ash>;
var azalea_flowers_iw = <item:immersive_weathering:azalea_flowers>;
var azalea_flowers_twigs = <item:twigs:azalea_flowers>;
var bamboo = <item:minecraft:bamboo>;
var bark = <tag:items:immersive_weathering:bark>;
var belt_pouch = <item:toolbelt:pouch>;
var black_dye = <item:minecraft:black_dye>;
var blank_rune = <item:quark:blank_rune>;
var bloodstone = <item:twigs:bloodstone>;
var blue_ice = <item:minecraft:blue_ice>;
var blue_sand = <item:byg:blue_sand>;
var book = <item:minecraft:book>;
var bound_scroll = <item:waystones:bound_scroll>;
var bucket = <item:minecraft:bucket>;
var builder_remote = <item:storagenetwork:builder_remote>;
var bundle = <item:minecraft:bundle>;
var calcite = <item:minecraft:calcite>;
var calcite_bricks = <item:quark:calcite_bricks>;
var calcite_tiles = <item:twigs:polished_calcite_bricks>;
var chain_plating = <item:byg:chain_plating>;
var charcoal = <item:minecraft:charcoal>;
var charred_fence = <item:immersive_weathering:charred_fence>;
var charred_fence_gate = <item:immersive_weathering:charred_fence_gate>;
var charred_log = <item:immersive_weathering:charred_log>;
var charred_planks = <item:immersive_weathering:charred_planks>;
var charred_slab = <item:immersive_weathering:charred_slab>;
var charred_stairs = <item:immersive_weathering:charred_stairs>;
var chest = <item:minecraft:chest>;
var chests = <tag:items:balm:wooden_chests>;
var chiseled_tuff_bricks = <item:quark:chiseled_tuff_bricks>;
var clay = <item:minecraft:clay>;
var clay_ball = <item:minecraft:clay_ball>;
var coal = <item:minecraft:coal>;
var coals = <tag:items:minecraft:coals>;
var cobblestone = <item:minecraft:cobblestone>;
var cobblestone_bricks = <item:quark:cobblestone_bricks>;
var cobblestone_tiles = <item:twigs:cobblestone_bricks>;
var collector = <item:storagenetwork:collector>;
var copper_block = <item:minecraft:copper_block>;
var copper_ingot = <item:minecraft:copper_ingot>;
var copper_patina = <item:additionaladditions:copper_patina>;
var crafting_remote = <item:storagenetwork:crafting_remote>;
var crafting_table = <item:minecraft:crafting_table>;
var crafting_table_stick = <item:crafting_on_a_stick:crafting_table>;
var crafting_tables = <tag:items:c:workbenches>;
var dacite = <item:byg:dacite>;
var diamond = <item:minecraft:diamond>;
var diamond_block = <item:minecraft:diamond_block>;
var diamond_horse_armor = <item:minecraft:diamond_horse_armor>;
var diamond_wand = <item:constructionwand:diamond_wand>;
var dirt = <item:minecraft:dirt>;
var dispenser = <item:minecraft:dispenser>;
var dropper = <item:minecraft:dropper>;
var emerald = <item:minecraft:emerald>;
var emeraldite_shards = <item:byg:emeraldite_shards>;
var end_rod = <item:minecraft:end_rod>;
var end_portal_frame = <item:minecraft:end_portal_frame>;
var end_stone = <item:minecraft:end_stone>;
var ender_chest = <item:minecraft:ender_chest>;
var ender_eye = <item:minecraft:ender_eye>;
var ender_pearl = <item:minecraft:ender_pearl>;
var exchange = <item:storagenetwork:exchange>;
var feral_flare_lantern = <item:torchmaster:feral_flare_lantern>;
var flax = <item:supplementaries:flax>;
var flint = <item:minecraft:flint>;
var flowers = <tag:items:minecraft:flowers>;
var glass = <item:minecraft:glass>;
var glass_panes = <tag:items:forge:glass_panes>;
var glowstone = <item:minecraft:glowstone>;
var glowstone_dust = <item:minecraft:glowstone_dust>;
var gold_horse_armor = <item:minecraft:golden_horse_armor>;
var gold_ingot = <item:minecraft:gold_ingot>;
var gold_nugget = <item:minecraft:gold_nugget>;
var honey_bottle = <item:minecraft:honey_bottle>;
var honeycomb = <item:minecraft:honeycomb>;
var hopper = <item:minecraft:hopper>;
var ice = <item:minecraft:ice>;
var infinite_water = <item:infinite_water:infinite_water>;
var iron_horse_armor = <item:minecraft:iron_horse_armor>;
var iron_ingot = <item:minecraft:iron_ingot>;
var iron_nugget = <item:minecraft:iron_nugget>;
var iron_rod = <item:quark:iron_rod>;
var lapis_block = <item:minecraft:lapis_block>;
var leather = <item:minecraft:leather>;
var leaves = <tag:items:minecraft:leaves>;
var logs = <tag:items:minecraft:logs>;
var mangrove_chest = <item:everycomp:q/wildbackport/mangrove_chest>;
var mangrove_logs = <tag:items:wildbackport:mangrove_logs>;
var megatorch = <item:torchmaster:megatorch>;
var mimicream = <item:alexsmobs:mimicream>;
var mossy_cobblestone_bricks = <item:quark:mossy_cobblestone_bricks>;
var mossy_cobblestone_tiles = <item:twigs:mossy_cobblestone_bricks>;
var mossy_red_rock_bricks = <item:byg:mossy_red_rock_bricks>;
var mossy_stone_bricks = <item:minecraft:mossy_stone_bricks>;
var mossy_stone_byg = <item:byg:mossy_stone>;
var mossy_stone_iw = <item:immersive_weathering:mossy_stone>;
var mud_byg = <item:byg:mud_block>;
var mud_wb = <item:wildbackport:mud>;
var mulch = <item:immersive_weathering:mulch_block>;
var nether_star = <item:minecraft:nether_star>;
var netherite_boots = <item:minecraft:netherite_boots>;
var netherite_chestplate = <item:minecraft:netherite_chestplate>;
var netherite_helmet = <item:minecraft:netherite_helmet>;
var netherite_horse_armor = <item:morehorsearmor:netherite_horse_armor>;
var netherite_ingot = <item:minecraft:netherite_ingot>;
var netherite_leggings = <item:minecraft:netherite_leggings>;
var netherite_pickarang = <item:quark:pickarang>;
var netherite_wand = <item:constructionwand:stone_wand>;
var network_cable = <item:storagenetwork:kabel>;
var network_root = <item:storagenetwork:master>;
var nulch = <item:immersive_weathering:nulch_block>;
var obsidian = <item:minecraft:obsidian>;
var packed_ice = <item:minecraft:packed_ice>;
var paper = <item:minecraft:paper>;
var peat = <item:byg:peat>;
var pebble = <item:twigs:pebble>;
var pendorite_boots = <item:byg:ametrine_boots>;
var pendorite_chestplate = <item:byg:ametrine_chestplate>;
var pendorite_helmet = <item:byg:ametrine_helmet>;
var pendorite_horse_armor = <item:byg:pendorite_horse_armor>;
var pendorite_ingot = <item:byg:pendorite_ingot>;
var pendorite_leggings = <item:byg:ametrine_leggings>;
var pendorite_pickarang = <item:quark:flamerang>;
var pendorite_wand = <item:constructionwand:infinity_wand>;
var piston = <item:minecraft:piston>;
var planks = <tag:items:minecraft:planks>;
var polished_calcite = <item:twigs:polished_calcite>;
var polished_tuff = <item:twigs:polished_tuff>;
var polished_tuff_slab_q = <item:quark:polished_tuff_slab>;
var polished_tuff_slab_tw = <item:twigs:polished_tuff_slab>;
var purple_dye = <item:minecraft:purple_dye>;
var quartz = <item:minecraft:quartz>;
var quartz_block = <item:minecraft:quartz_block>;
var quartz_crystal_byg = <item:byg:quartz_crystal>;
var quartzite_sand = <item:byg:quartzite_sand>;
var rabbit_hide = <item:minecraft:rabbit_hide>;
var rainbow_jelly = <item:alexsmobs:rainbow_jelly>;
var rainbow_rune = <item:quark:rainbow_rune>;
var raw_pendorite_block = <item:byg:raw_pendorite_block>;
var raw_quartz_block = <item:byg:raw_quartz_block>;
var red_rock = <item:byg:red_rock>;
var red_rock_bricks = <item:byg:red_rock_bricks>;
var red_sand = <item:minecraft:red_sand>;
var redstone_block = <item:minecraft:redstone_block>;
var return_scroll = <item:waystones:return_scroll>;
var rhyolite = <item:twigs:rhyolite>;
var ring_of_attraction = <item:ring_of_attraction:ring_of_attraction>;
var ring_of_enderchest = <item:ring_of_enderchest:ring_of_enderchest>;
var rocky_dirt_db = <item:decorative_blocks:rocky_dirt>;
var rope = <item:quark:rope>;
var rope_bamboo_bridge = <item:everycomp:mcb/twigs/rope_bamboo_bridge>;
var saffron_guide = <item:patchouli:guide_book>.withTag({"patchouli:book": "patchouli:saffron_guide_book"});
var sawmill = <item:corail_woodcutter:oak_woodcutter>;
var scales = <tag:items:immersive_weathering:scales>;
var schist = <item:twigs:schist>;
var seed_pouch = <item:quark:seed_pouch>;
var shiny_chiseled_therium = <item:byg:shiny_chiseled_therium>;
var simple_shop = <item:simpleshops:simple_shop>;
var singleton_upgrade = <item:storagenetwork:single_upgrade>;
var slingshot = <item:supplementaries:slingshot>;
var smooth_basalt = <item:minecraft:smooth_basalt>;
var snow_block = <item:minecraft:snow_block>;
var snowball = <item:minecraft:snowball>;
var stick = <item:minecraft:stick>;
var sticky_piston = <item:minecraft:sticky_piston>;
var stock_upgrade = <item:storagenetwork:stock_upgrade>;
var stone = <item:minecraft:stone>;
var stone_bricks = <item:minecraft:stone_bricks>;
var stone_crafting_materials = <tag:items:minecraft:stone_crafting_materials>;
var stone_tag = <tag:items:forge:stone>;
var soapstone = <item:byg:soapstone>;
var storage_inventory = <item:storagenetwork:inventory>;
var storage_remote = <item:storagenetwork:inventory_remote>;
var storage_request = <item:storagenetwork:request>;
var storage_upgrade_1 = <item:storagedrawers:obsidian_storage_upgrade>;
var storage_upgrade_2 = <item:storagedrawers:iron_storage_upgrade>;
var storage_upgrade_3 = <item:storagedrawers:gold_storage_upgrade>;
var storage_upgrade_4 = <item:storagedrawers:diamond_storage_upgrade>;
var storage_upgrade_5 = <item:storagedrawers:emerald_storage_upgrade>;
var string_item = <item:minecraft:string>;
var tool_belt = <item:toolbelt:belt>;
var torch = <item:minecraft:torch>;
var totem = <item:minecraft:totem_of_undying>;
var travertine = <item:byg:travertine>;
var tuff = <item:minecraft:tuff>;
var tuff_brick_slab_q = <item:quark:tuff_bricks_slab>;
var tuff_brick_slab_tw = <item:twigs:polished_tuff_brick_slab>;
var tuff_bricks = <item:twigs:polished_tuff_bricks>;
var tuff_pillar = <item:quark:tuff_pillar>;
var upper = <item:uppers:upper>;
var uprade_template = <item:storagedrawers:upgrade_template>;
var void_totem = <item:voidtotem:totem_of_void_undying>;
var warp_dust = <item:waystones:warp_dust>;
var warp_plate = <item:waystones:warp_plate>;
var warp_scroll = <item:waystones:warp_scroll>;
var warp_stone = <item:waystones:warp_stone>;
var water_bucket = <item:minecraft:water_bucket>;
var wax = <tag:items:immersive_weathering:wax>;
var wrappist_shard = <item:geode:wrappist_shard>;
var bamboo_block = <item:twigs:bundled_bamboo>;
var stripped_bamboo_block = <item:twigs:stripped_bundled_bamboo>;
var stripped_bamboo = <item:twigs:stripped_bamboo>;
var bamboo_planks = <item:one_twenty_backport:bamboo_planks>;
var bamboo_logs = <tag:items:twigs:bamboo_logs>;
var stripped_bamboo_bridge = <item:mcwbridges:dry_bamboo_bridge>;
var stripped_bamboo_support = <item:mcwbridges:dry_bamboo_bridge_pier>;
var quiver = <item:supplementaries:quiver>;
var ether_stone = <item:byg:ether_stone>;
var soul_torch = <item:minecraft:soul_torch>;
var fire_charge = <item:minecraft:fire_charge>;
var myalite = <item:quark:myalite>;
var purpur_stone = <item:byg:purpur_stone>;
var permafrost = <item:quark:permafrost>;
var limestone = <item:quark:limestone>;
var jasper = <item:quark:jasper>;
var diorite = <item:minecraft:diorite>;
var deepslate = <item:minecraft:deepslate>;
var flint_block = <item:supplementaries:flint_block>;
var shale = <item:quark:shale>;
var saddle = <item:minecraft:saddle>;
var name_tag = <item:minecraft:name_tag>;
var emeraldite_block = <item:contenttweaker:emeraldite_block>;
var pendorite_scraps = <item:byg:pendorite_scraps>;
var pendorite_scrap_block = <item:contenttweaker:pendorite_scrap_block>;
var raw_pendorite = <item:byg:raw_pendorite>;

//add tags
<tag:items:immersive_weathering:soot>.addId(<resource:immersive_weathering:soot>);
<tag:items:twigs:bamboo_logs>.addId(bamboo_block.registryName, stripped_bamboo_block.registryName);
<tag:items:c:workbenches>.add(<tag:items:forge:workbench>, <tag:items:mctb:revertable_workbench>);
<tag:items:c:workbenches>.addId(<resource:minecraft:crafting_table>);
<tag:items:minecraft:scute>.addId(<resource:minecraft:scute>, <resource:alexsmobs:crocodile_scute>, <resource:alexsmobs:spiked_scute>);
<tag:items:quark:big_harvesting_hoes>.addId(<resource:byg:pendorite_hoe>);
<tag:items:minecraft:planks>.addId(bamboo_planks.registryName, <resource:one_twenty_backport:bamboo_mosaic_planks>);
<tag:items:forge:hoes>.addId(<resource:additionaladditions:rose_gold_hoe>);
<tag:items:forge:ores/quartz>.addId(<resource:byg:blue_nether_quartz_ore>, <resource:byg:brimstone_nether_quartz_ore>);
<tag:items:forge:stone>.addId(<resource:byg:travertine>, <resource:twigs:rhyolite>, <resource:twigs:schist>, <resource:twigs:bloodstone>, <resource:minecraft:dripstone_block>, <resource:minecraft:calcite>);
<tag:items:minecraft:stone_tool_materials>.addId(<resource:twigs:bloodstone>);
<tag:items:quark:stone_tool_materials>.add(<tag:items:minecraft:stone_tool_materials>);
<tag:items:doubleslabs:disabled_slabs>.addId(<resource:minecraft:campfire>, <resource:minecraft:soul_campfire>, <resource:byg:boric_campfire>, <resource:byg:cryptic_campfire>);

<tag:blocks:forge:glass>.addId(<resource:byg:therium_glass>, <resource:alexsmobs:rainbow_glass>);
<tag:items:forge:glass>.addId(<resource:byg:therium_glass>, <resource:alexsmobs:rainbow_glass>);
<tag:blocks:forge:glass_panes>.addId(<resource:byg:therium_glass_pane>);
<tag:items:forge:glass_panes>.addId(<resource:byg:therium_glass_pane>);
<tag:blocks:minecraft:mineable/pickaxe>.add(<tag:blocks:forge:glass>, <tag:blocks:forge:glass_panes>);
<tag:blocks:minecraft:mineable/pickaxe>.addId(<resource:minecraft:end_portal_frame>, <resource:contenttweaker:pendorite_scrap_block>, <resource:contenttweaker:emeraldite_block>);
<tag:blocks:minecraft:needs_diamond_tool>.addId(<resource:contenttweaker:pendorite_scrap_block>);
<tag:blocks:minecraft:beacon_base_blocks>.addId(<resource:contenttweaker:emeraldite_block>);

<tag:items:quark:vertical_slabs>.addId(<resource:supplementaries:ash_bricks_vertical_slab>, <resource:supplementaries:stone_tile_vertical_slab>, <resource:supplementaries:blackstone_tile_vertical_slab>, <resource:supplementaries:checker_vertical_slab>,
    <resource:quark:dripstone_block_vertical_slab>, <resource:quark:polished_dripstone_vertical_slab>, <resource:quark:tuff_vertical_slab>, <resource:quark:polished_tuff_vertical_slab>, <resource:quark:dripstone_bricks_vertical_slab>, <resource:quark:tuff_bricks_vertical_slab>,
    <resource:supplementaries:lapis_bricks_vertical_slab>, <resource:dustrial_decor:mini_padded_vertical_slab>, <resource:dustrial_decor:cast_iron_brick_vertical_slab>, <resource:dustrial_decor:sheet_metal_plating_vertical_slab>, <resource:dustrial_decor:sheet_metal_treading_vertical_slab>,
    <resource:dustrial_decor:rusty_sheet_metal_plating_vertical_slab>, <resource:dustrial_decor:rusty_sheet_metal_treading_vertical_slab>, <resource:dustrial_decor:cinder_brick_vertical_slab>, <resource:dustrial_decor:smooth_cardboard_vertical_slab>,
    <resource:dustrial_decor:bolted_industrial_iron_vertical_slab>);
<tag:items:quark:vertical_slab>.add(<tag:items:quark:wooden_vertical_slabs>);

var colors = ["white", "orange", "magenta", "light_blue", "yellow", "lime", "pink", "gray", "light_gray", "cyan", "purple", "blue", "brown", "green", "red", "black"];

//remove tags
for color in colors {
	<tagmanager:items>.tag(<resource:balm:${color}_dyes>).removeId(<resource:chalk:${color}_chalk>);
	<tagmanager:items>.tag(<resource:forge:dyes/${color}>).removeId(<resource:chalk:${color}_chalk>);
	<tag:items:balm:dyes>.removeId(<resource:chalk:${color}_chalk>);
	<tag:items:forge:dyes>.removeId(<resource:chalk:${color}_chalk>);

	<tag:items:comforts:hammocks>.addId(<resource:comforts:hammock_${color}>);
	<tag:items:comforts:sleeping_bags>.addId(<resource:comforts:sleeping_bag_${color}>);

	<tag:items:quark:colored_runes>.addId(<resource:quark:${color}_rune>);
}


//remove recipes
stoneCutter.removeByName("twigs:copper/cut_copper_slab_from_stonecutting");
craftingTable.removeByName("mctb:workbench_revert");
craftingTable.removeByName("quark:building/crafting/rusty_iron_plate");
craftingTable.removeByName("twigs:misc/cobblestone_from_pebble");
craftingTable.removeByName("byg:scoria_stone_from_crafting");
craftingTable.removeByName("infinite_water:lava");
craftingTable.removeByName("infinite_water:milk");
craftingTable.removeByName("supplementaries:bubble_blower");
craftingTable.removeByRegex("immersive_weathering.*from_bark");
craftingTable.removeByRegex("immersive_weathering.*unstrip");
craftingTable.removeByName("immersive_weathering:paper_from_birch_bark");

//craftable guide
craftingTable.addShapeless("saffron_guide", saffron_guide, [book, flowers]);

//contenttweaker recipes
craftingTable.addShapeless("emeraldite_from_block", emeraldite_shards * 9, [emeraldite_block]);
craftingTable.addShaped("emeraldite_to_block", emeraldite_block,
[[emeraldite_shards, emeraldite_shards, emeraldite_shards],
[emeraldite_shards, emeraldite_shards, emeraldite_shards],
[emeraldite_shards, emeraldite_shards, emeraldite_shards]]);

craftingTable.addShapeless("pendorite_scraps_from_block", pendorite_scraps * 9, [pendorite_scrap_block]);
craftingTable.addShaped("pendorite_scraps_to_block", pendorite_scrap_block,
[[pendorite_scraps, pendorite_scraps, pendorite_scraps],
[pendorite_scraps, pendorite_scraps, pendorite_scraps],
[pendorite_scraps, pendorite_scraps, pendorite_scraps]]);

//change recipes using empty tags
craftingTable.removeByName("byg:brown_dye");
Replacer.forOutput(mulch, craftingTable).replace(bark, leaves).execute();
Replacer.forOutput(nulch, craftingTable).replace(scales, leaves).execute();

//make storage upgrades require the previous tier
Replacer.forOutput(storage_upgrade_1, craftingTable).replace(obsidian, stone_tag).execute();
Replacer.forOutput(storage_upgrade_2, craftingTable).replace(uprade_template, storage_upgrade_1).execute();
Replacer.forOutput(storage_upgrade_3, craftingTable).replace(uprade_template, storage_upgrade_2).execute();
Replacer.forOutput(storage_upgrade_4, craftingTable).replace(uprade_template, storage_upgrade_3).execute();
Replacer.forOutput(storage_upgrade_5, craftingTable).replace(uprade_template, storage_upgrade_4).execute();

//replace rabbit hide with leather
Replacer.forOutput(bundle, craftingTable).replace(rabbit_hide, leather).execute();
Replacer.forOutput(slingshot, craftingTable).replace(rabbit_hide, leather).execute();
Replacer.forOutput(seed_pouch, craftingTable).replace(rabbit_hide, leather).execute();

//better iron rod
Replacer.forOutput(iron_rod, craftingTable).replace(end_rod, iron_ingot).execute();

//proper bamboo implementation
craftingTable.remove(bamboo_block);
craftingTable.addShaped("bamboo_block", bamboo_block, [[bamboo, bamboo, bamboo], [bamboo, bamboo, bamboo], [bamboo, bamboo, bamboo]]);
craftingTable.remove(bamboo);
craftingTable.addShapeless("bamboo", bamboo * 9, [bamboo_block]);
craftingTable.remove(stripped_bamboo_block);
craftingTable.addShaped("stripped_bamboo_block", stripped_bamboo_block, [[stripped_bamboo, stripped_bamboo, stripped_bamboo], [stripped_bamboo, stripped_bamboo, stripped_bamboo], [stripped_bamboo, stripped_bamboo, stripped_bamboo]]);
craftingTable.remove(stripped_bamboo);
craftingTable.addShapeless("stripped_bamboo", stripped_bamboo * 9, [stripped_bamboo_block]);
craftingTable.remove(bamboo_planks);
craftingTable.addShapeless("bamboo_planks", bamboo_planks * 2, [bamboo_logs]);
craftingTable.remove(stripped_bamboo_bridge);
craftingTable.addShaped("stripped_bamboo_bridge", stripped_bamboo_bridge,
	[[stripped_bamboo, string_item, stripped_bamboo],
	[stripped_bamboo, stripped_bamboo, stripped_bamboo]]);
craftingTable.remove(stripped_bamboo_support);
craftingTable.addShaped("stripped_bamboo_support", stripped_bamboo_support,
	[[stripped_bamboo, string_item, stripped_bamboo],
	[stripped_bamboo, air, stripped_bamboo]]);
craftingTable.addShaped("stick_from_stripped_bamboo", stick, [[stripped_bamboo], [stripped_bamboo]]);

//azalea flowers compat
craftingTable.addShapeless("azalea_flowers", azalea_flowers_twigs, [azalea_flowers_iw]);
craftingTable.addShapeless("azalea_flowers_reverse", azalea_flowers_iw, [azalea_flowers_twigs]);

//changed sawmill recipe
craftingTable.remove(sawmill);
craftingTable.addShaped("sawmill", sawmill,
	[[air, iron_ingot, air],
	[stick, stone_tag, stick]]);

//unpacking stuff
craftingTable.addShapeless("unpacking_clay", clay_ball * 4, [clay]);
craftingTable.addShapeless("unpacking_glowstone", glowstone_dust * 4, [glowstone]);
craftingTable.addShapeless("unpacking_snow", snowball * 4, [snow_block]);
craftingTable.addShapeless("unpacking_quartz", quartz * 4, [quartz_block]);
craftingTable.addShapeless("unpacking_blue_ice", packed_ice * 9, [blue_ice]);
craftingTable.addShapeless("unpacking_packed_ice", ice * 9, [packed_ice]);

//renewable pebble
stoneCutter.addRecipe("pebble", pebble * 4, cobblestone);

//rocky dirt from db like from twigs
craftingTable.remove(rocky_dirt_db);
craftingTable.addShaped("rocky_dirt_db", rocky_dirt_db * 2,
	[[pebble, dirt],
	[dirt, pebble]]);

//sticky piston with honey
craftingTable.addShaped("honey_sticky_piston", sticky_piston, [[honey_bottle], [piston]]);

//craftable quiver
craftingTable.addShapeless("quiver", quiver, [bundle, rope]);

//make all chests usable in recipes
Replacer.forOutput(simple_shop, craftingTable).replace(chest, chests).execute();
Replacer.forOutput(upper, craftingTable).replace(chest, chests).execute();
Replacer.forOutput(stock_upgrade, craftingTable).replace(chest, chests).execute();

//bricks from twigs
Replacer.forOutput(cobblestone_tiles, craftingTable).replace(cobblestone, cobblestone_bricks).execute();
Replacer.forOutput(calcite_tiles, craftingTable).replace(polished_calcite, calcite_bricks).execute();
Replacer.forOutput(tuff_pillar, craftingTable).replace(polished_tuff_slab_q, polished_tuff_slab_tw).execute();
Replacer.forOutput(chiseled_tuff_bricks, craftingTable).replace(tuff_brick_slab_q, tuff_brick_slab_tw).execute();

//twigs stonecutter recipes fix
stoneCutter.addRecipe("smooth_basalt_brick_stairs", <item:twigs:smooth_basalt_brick_stairs>, smooth_basalt);
stoneCutter.addRecipe("smooth_basalt_brick_slab", <item:twigs:smooth_basalt_brick_slab> * 2, smooth_basalt);
stoneCutter.addRecipe("smooth_basalt_brick_wall", <item:twigs:smooth_basalt_brick_wall>, smooth_basalt);
stoneCutter.addRecipe("cobblestone_bricks", cobblestone_tiles, cobblestone_bricks);
stoneCutter.addRecipe("cobblestone_brick_stairs", <item:twigs:cobblestone_brick_stairs>, cobblestone);
stoneCutter.addRecipe("cobblestone_brick_slab", <item:twigs:cobblestone_brick_slab> * 2, cobblestone);
stoneCutter.addRecipe("cobblestone_brick_wall", <item:twigs:cobblestone_brick_wall>, cobblestone);
stoneCutter.addRecipe("polished_amethyst_brick_stairs", <item:twigs:polished_amethyst_brick_stairs>, amethyst_block);
stoneCutter.addRecipe("polished_amethyst_brick_slab", <item:twigs:polished_amethyst_brick_slab> * 2, amethyst_block);
stoneCutter.addRecipe("polished_amethyst_brick_wall", <item:twigs:polished_amethyst_brick_wall>, amethyst_block);
stoneCutter.addRecipe("polished_amethyst_stairs", <item:twigs:polished_amethyst_stairs>, amethyst_block);
stoneCutter.addRecipe("polished_amethyst_slab", <item:twigs:polished_amethyst_slab> * 2, amethyst_block);
stoneCutter.remove(tuff_bricks);
stoneCutter.addRecipe("polished_tuff_bricks_from_polished_tuff", tuff_bricks, polished_tuff);
stoneCutter.addRecipe("polished_tuff_bricks_from_tuff", tuff_bricks, tuff);
stoneCutter.addRecipe("polished_tuff_brick_stairs", <item:twigs:polished_tuff_brick_stairs>, tuff);
stoneCutter.addRecipe("polished_tuff_brick_slab", tuff_brick_slab_tw * 2, tuff);
stoneCutter.addRecipe("polished_tuff_brick_wall", <item:twigs:polished_tuff_brick_wall>, tuff);
stoneCutter.addRecipe("polished_tuff_stairs", <item:twigs:polished_tuff_stairs>, tuff);
stoneCutter.addRecipe("polished_tuff_slab", polished_tuff_slab_tw * 2, tuff);
stoneCutter.remove(calcite_tiles);
stoneCutter.addRecipe("polished_calcite_bricks_from_polished_calcite", calcite_tiles, calcite_bricks);
stoneCutter.addRecipe("polished_calcite_bricks_from_calcite", calcite_tiles, calcite);
stoneCutter.addRecipe("polished_calcite_brick_stairs", <item:twigs:polished_calcite_brick_stairs>, calcite);
stoneCutter.addRecipe("polished_calcite_brick_slab", <item:twigs:polished_calcite_brick_slab> * 2, calcite);
stoneCutter.addRecipe("polished_calcite_brick_wall", <item:twigs:polished_calcite_brick_wall>, calcite);
stoneCutter.addRecipe("polished_rhyolite_bricks", <item:twigs:polished_rhyolite_bricks>, rhyolite);
stoneCutter.addRecipe("polished_rhyolite_brick_stairs", <item:twigs:polished_rhyolite_brick_stairs>, rhyolite);
stoneCutter.addRecipe("polished_rhyolite_brick_slab", <item:twigs:polished_rhyolite_brick_slab> * 2, rhyolite);
stoneCutter.addRecipe("polished_rhyolite_brick_wall", <item:twigs:polished_rhyolite_brick_wall>, rhyolite);
stoneCutter.addRecipe("polished_rhyolite_stairs", <item:twigs:polished_rhyolite_stairs>, rhyolite);
stoneCutter.addRecipe("polished_rhyolite_slab", <item:twigs:polished_rhyolite_slab> * 2, rhyolite);
stoneCutter.addRecipe("polished_schist_bricks", <item:twigs:polished_schist_bricks>, schist);
stoneCutter.addRecipe("polished_schist_brick_stairs", <item:twigs:polished_schist_brick_stairs>, schist);
stoneCutter.addRecipe("polished_schist_brick_slab", <item:twigs:polished_schist_brick_slab> * 2, schist);
stoneCutter.addRecipe("polished_schist_brick_wall", <item:twigs:polished_schist_brick_wall>, schist);
stoneCutter.addRecipe("polished_schist_stairs", <item:twigs:polished_schist_stairs>, schist);
stoneCutter.addRecipe("polished_schist_slab", <item:twigs:polished_schist_slab> * 2, schist);
stoneCutter.addRecipe("polished_bloodstone_bricks", <item:twigs:polished_bloodstone_bricks>, bloodstone);
stoneCutter.addRecipe("polished_bloodstone_brick_stairs", <item:twigs:polished_bloodstone_brick_stairs>, bloodstone);
stoneCutter.addRecipe("polished_bloodstone_brick_slab", <item:twigs:polished_bloodstone_brick_slab> * 2, bloodstone);
stoneCutter.addRecipe("polished_bloodstone_brick_wall", <item:twigs:polished_bloodstone_brick_wall>, bloodstone);
stoneCutter.addRecipe("polished_bloodstone_stairs", <item:twigs:polished_bloodstone_stairs>, bloodstone);
stoneCutter.addRecipe("polished_bloodstone_slab", <item:twigs:polished_bloodstone_slab> * 2, bloodstone);

//charred planks implementation
<tag:items:minecraft:flammable_planks>.add(<tag:items:minecraft:planks>);
<tag:items:minecraft:flammable_planks>.removeId(<resource:minecraft:crimson_planks>, <resource:minecraft:warped_planks>, <resource:byg:lament_planks>, <resource:byg:sythian_planks>, <resource:byg:embur_planks>);
Replacer.forOutput(charred_planks, furnace).replace(<tag:items:immersive_weathering:non_flammable_planks>, <tag:items:minecraft:flammable_planks>).execute();
furnace.addRecipe("log_to_charred_log", charred_log, <tag:items:minecraft:logs_that_burn>, 0.1, 200);
furnace.addRecipe("charred_planks_to_ash", ash_iw, charred_planks, 0.1, 200);
craftingTable.addShapeless("charred_planks_from_charred_log", charred_planks * 4, [charred_log]);
furnace.remove(charred_slab);
craftingTable.addShaped("charred_slab", charred_slab * 6, [[charred_planks, charred_planks, charred_planks]]);
furnace.remove(charred_stairs);
craftingTable.addShaped("charred_stairs", charred_stairs * 4,
[[charred_planks, air, air],
[charred_planks, charred_planks, air],
[charred_planks, charred_planks, charred_planks]]);
furnace.remove(charred_fence);
craftingTable.addShaped("charred_fence", charred_fence * 3,
[[charred_planks, stick, charred_planks],
[charred_planks, stick, charred_planks]]);
furnace.remove(charred_fence_gate);
craftingTable.addShaped("charred_fence_gate", charred_fence_gate,
[[stick, charred_planks, stick],
[stick, charred_planks, stick]]);
furnace.remove(charcoal);
furnace.addRecipe("charcoal", charcoal, charred_log, 0.1, 200);

//simple storage recipes made harder
craftingTable.remove(network_cable);
craftingTable.addShaped("network_cable", network_cable * 3, [[copper_ingot, copper_ingot, copper_ingot]]);
craftingTable.remove(network_root);
craftingTable.addShaped("network_root", network_root,
[[gold_ingot, network_cable, gold_ingot],
[network_cable, diamond, network_cable],
[gold_ingot, network_cable, gold_ingot]]);
craftingTable.remove(storage_inventory);
craftingTable.addShaped("storage_inventory", storage_inventory,
[[gold_ingot, network_cable, gold_ingot],
[network_cable, chests, network_cable],
[gold_ingot, network_cable, gold_ingot]]);
craftingTable.remove(storage_request);
craftingTable.addShaped("storage_request", storage_request,
[[gold_ingot, crafting_tables, gold_ingot],
[crafting_tables, storage_inventory, crafting_tables],
[gold_ingot, netherite_ingot, gold_ingot]]);
Replacer.forOutput(exchange, craftingTable).replace(iron_nugget, gold_ingot).execute();
Replacer.forOutput(collector, craftingTable).replace(iron_nugget, gold_ingot).execute();
craftingTable.remove(storage_remote);
craftingTable.addShaped("storage_remote", storage_remote,
[[copper_block, ender_chest, copper_block],
[black_dye, network_root, black_dye],
[copper_block, pendorite_ingot, copper_block]]);
craftingTable.remove(crafting_remote);
craftingTable.addShaped("crafting_remote", crafting_remote,
[[ametrine_block, crafting_tables, ametrine_block],
[crafting_tables, storage_remote, crafting_tables],
[ametrine_block, crafting_tables, ametrine_block]]);
craftingTable.remove(builder_remote);
craftingTable.addShaped("builder_remote", builder_remote,
[[redstone_block, dispenser, redstone_block],
[dispenser, storage_remote, dispenser],
[redstone_block, dispenser, redstone_block]]);

//waystones made harder
Replacer.forOutput(warp_stone, craftingTable).replace(purple_dye, warp_dust).replace(emerald, diamond).execute();
Replacer.forOutput(return_scroll, craftingTable).replace(purple_dye, warp_dust).execute();
craftingTable.remove(bound_scroll);
craftingTable.addShaped("bound_scroll", bound_scroll * 3, 
	[[warp_dust, gold_nugget, warp_dust],
	[paper, paper, paper]]);
craftingTable.remove(warp_scroll);
craftingTable.addShapeless("warp_scroll", warp_scroll, [bound_scroll, ender_pearl]);
craftingTable.remove(warp_dust);
craftingTable.addShapeless("warp_dust", warp_dust, [ender_pearl, purple_dye]);
craftingTable.remove(warp_plate);
craftingTable.addShaped("warp_plate", warp_plate, 
	[[warp_dust, flint, warp_dust],
	[stone_bricks, stone_bricks, stone_bricks],
	[warp_dust, ametrine_block, warp_dust]]);

//torchmaster made harder
craftingTable.remove(megatorch);
craftingTable.addShaped("megatorch", megatorch,
[[glowstone, glowstone, glowstone],
[planks, nether_star, planks],
[planks, planks, planks]]);
craftingTable.remove(feral_flare_lantern);
craftingTable.addShaped("feral_flare_lantern", feral_flare_lantern,
[[iron_ingot, glass_panes, iron_ingot],
[glass_panes, glowstone, glass_panes],
[iron_ingot, glass_panes, iron_ingot]]);

//infinite water made harder
craftingTable.remove(infinite_water);
craftingTable.addShaped("infinite_water", infinite_water,
[[water_bucket, netherite_ingot, water_bucket],
[air, bucket, air]]);

//toblet made harder
Replacer.forOutput(tool_belt, craftingTable).replace(iron_ingot, diamond).execute();
Replacer.forOutput(belt_pouch, craftingTable).replace(gold_ingot, netherite_ingot).execute();

//use new craftingtables like the vanilla one
Replacer.forOutput(crafting_table_stick, craftingTable).replace(crafting_table, crafting_tables).execute();

//coal integration
craftingTable.removeByName("byg:peat_from_crafting2");
Replacer.forOutput(singleton_upgrade, craftingTable).replace(coal, coals).execute();
Replacer.forOutput(torch, craftingTable).replace(coal, coals).execute();
Replacer.forOutput(soul_torch, craftingTable).replace(coal, coals).execute();
Replacer.forOutput(fire_charge, craftingTable).replace(coal, coals).execute();

//byg sand smelting to regular glass
furnace.removeByRegex("byg.*_glass_from_sand");

//byg quartz stuff
furnace.addRecipe("quartzite_sand_to_glass", glass, quartzite_sand, 0.1, 200);
furnace.addRecipe("raw_quartz_block_to_quartz", quartz, raw_quartz_block, 0.1, 200);
craftingTable.addShaped("raw_quartz_block", raw_quartz_block,
[[quartz_crystal_byg, quartz_crystal_byg],
[quartz_crystal_byg, quartz_crystal_byg]]);

//proper implementation of mossy stones
function mossySpecific(input as IItemStack, output as IItemStack, doVine as bool) as void {
	var outName = output.registryName.namespace + "-" + output.registryName.path;
	craftingTable.addShapeless(outName + "_from_moss_clump", output, [input, <item:immersive_weathering:moss_clump>]);
	craftingTable.addShapeless(outName + "_from_moss_block", output, [input, <item:minecraft:moss_block>]);
	if (doVine) {
		craftingTable.addShapeless(outName + "_from_vines", output, [input, <item:minecraft:vine>]);
	}
}

function mossySimple(mod as string, id as string, doVine as bool) as void {
	var outId = "mossy_" + id;
	mossySpecific(<item:${mod}:${id}>, <item:${mod}:${outId}>, doVine);
}

function mossy(mod as string, id as string) as void {
	mossySimple(mod, id, true);
}

Replacer.forOutput(mossy_stone_bricks, craftingTable).replace(mossy_stone_byg, mossy_stone_iw).execute();
mossy("quark", "cobblestone_bricks");
craftingTable.remove(mossy_cobblestone_tiles);
mossy("twigs", "cobblestone_bricks");
craftingTable.addShaped("mossy_cobblestone_tiles_from_mossy_cobblestone_bricks", mossy_cobblestone_tiles * 4,
[[mossy_cobblestone_bricks, mossy_cobblestone_bricks],
[mossy_cobblestone_bricks, mossy_cobblestone_bricks]]);
mossySimple("byg", "red_rock_bricks", false);

//proper construction wand implementation
craftingTable.remove(netherite_wand);
smithing.addRecipe("netherite_wand", netherite_wand, diamond_wand, netherite_ingot);
craftingTable.remove(pendorite_wand);
smithing.addRecipe("pendorite_wand", pendorite_wand, netherite_wand, pendorite_ingot);

//proper pickarang implementation
Replacer.forOutput(netherite_pickarang, craftingTable).replace(diamond, netherite_ingot).execute();
Replacer.forOutput(pendorite_pickarang, smithing).replace(netherite_ingot, pendorite_ingot).execute();

//horse armor recipes
craftingTable.remove(pendorite_horse_armor);
smithing.addRecipe("pendorite_horse_armor", pendorite_horse_armor, netherite_horse_armor, pendorite_ingot);
craftingTable.addShaped("iron_horse_armor", iron_horse_armor,
[[iron_ingot, air, iron_ingot],
[iron_ingot, iron_ingot, iron_ingot],
[iron_ingot, air, iron_ingot]]);
craftingTable.addShaped("gold_horse_armor", gold_horse_armor,
[[gold_ingot, air, gold_ingot],
[gold_ingot, gold_ingot, gold_ingot],
[gold_ingot, air, gold_ingot]]);
craftingTable.addShaped("diamond_horse_armor", diamond_horse_armor,
[[diamond, air, diamond],
[diamond, diamond, diamond],
[diamond, air, diamond]]);

//craftable saddle
craftingTable.addShaped("saddle", saddle,
[[leather, leather, leather],
[string_item, leather, string_item],
[iron_ingot, air, iron_ingot]]);

//craftable nametag
craftingTable.addShaped("name_tag", name_tag, [[paper, paper, string_item]]);

//byg balancing
craftingTable.removeByName("byg:emerald_from_emeraldite");
craftingTable.addShapeless("emeralds_from_emeraldite", emerald * 2, [emeraldite_shards]);
craftingTable.remove(chain_plating);
craftingTable.addShaped("chain_plating", chain_plating * 4,
[[iron_ingot, iron_nugget],
[iron_nugget, iron_ingot]]);

//better rainbow rune
craftingTable.remove(rainbow_rune);
craftingTable.addShapeless("rainbow_rune", rainbow_rune, [blank_rune, rainbow_jelly]);

//quark rope like supplementaries
craftingTable.remove(rope);
craftingTable.addShaped("rope", rope,[[flax], [flax], [flax]]);

//remove blue sand recipe
craftingTable.remove(blue_sand);

//wrappist to copper patina
craftingTable.addShapeless("copper_patina_from_wrappist", copper_patina * 4, [copper_ingot, wrappist_shard]);

//properly implement pendorite as netherite upgrade
smithing.remove(pendorite_boots);
smithing.addRecipe("pendorite_boots", pendorite_boots, netherite_boots, pendorite_ingot);
smithing.remove(pendorite_chestplate);
smithing.addRecipe("pendorite_chestplate", pendorite_chestplate, netherite_chestplate, pendorite_ingot);
smithing.remove(pendorite_helmet);
smithing.addRecipe("pendorite_helmet", pendorite_helmet, netherite_helmet, pendorite_ingot);
smithing.remove(pendorite_leggings);
smithing.addRecipe("pendorite_leggings", pendorite_leggings, netherite_leggings, pendorite_ingot);

//harder pendorite recipe
craftingTable.remove(pendorite_ingot);
craftingTable.addShapeless("pendorite_ingot", pendorite_ingot, [pendorite_scrap_block, ametrine_block, emeraldite_block, diamond_block]);

//ring recipes made harder
craftingTable.remove(ring_of_enderchest);
craftingTable.addShaped("ring_of_enderchest", ring_of_enderchest,
[[copper_block, ender_chest, copper_block],
[iron_ingot, air, iron_ingot],
[iron_ingot, iron_ingot, iron_ingot]]);
craftingTable.remove(ring_of_attraction);
craftingTable.addShaped("ring_of_attraction", ring_of_attraction,
[[redstone_block, copper_block, lapis_block],
[iron_ingot, air, iron_ingot],
[iron_ingot, iron_ingot, iron_ingot]]);

//make void totem not require a regular one
Replacer.forOutput(void_totem, craftingTable).replace(totem, water_bucket).execute();

//supplementaries ash -> iw ash
Replacer.forOutput(ash_brick, furnace).replace(ash_supplementaries, ash_iw).execute();

//iw tallow compat
Replacer.forRegexRecipes("(?!immersive_weathering:).*waxed_.*").replace(honeycomb, wax).execute();
Replacer.forOutput(shiny_chiseled_therium, craftingTable).replace(honeycomb, wax).execute();

//craftable end portal
craftingTable.addShaped("end_portal_frame", end_portal_frame,
[[mimicream, mimicream, mimicream],
[mimicream, ender_eye, mimicream],
[end_stone, end_stone, end_stone]]);

//remove kiln recipes
<recipetype:clayworks:baking>.removeByRegex(".*");

//proper excavated variants implementation
function provideMinecraftOreSmeltingCompat(id as string, name as string) as void {
    Replacer.forRegexRecipes("minecraft:" + id + "_from_(smelting|blasting)_" + name + "_ore").replace(<item:minecraft:${name}_ore>, <tagmanager:items>.tag(<resource:forge:ores/${name}>)).execute();
    furnace.removeByRegex("minecraft:" + id + "_from_smelting_deepslate_" + name + "_ore");
    blastFurnace.removeByRegex("minecraft:" + id + "_from_blasting_deepslate_" + name + "_ore");
}

provideMinecraftOreSmeltingCompat("coal", "coal");
provideMinecraftOreSmeltingCompat("copper_ingot", "copper");
provideMinecraftOreSmeltingCompat("gold_ingot", "gold");
provideMinecraftOreSmeltingCompat("redstone", "redstone");
provideMinecraftOreSmeltingCompat("diamond", "diamond");
provideMinecraftOreSmeltingCompat("iron_ingot", "iron");
provideMinecraftOreSmeltingCompat("lapis_lazuli", "lapis");
provideMinecraftOreSmeltingCompat("emerald", "emerald");

Replacer.forRegexRecipes("minecraft:quartz(_from_blasting)?").replace(<item:minecraft:nether_quartz_ore>, <tagmanager:items>.tag(<resource:forge:ores/quartz>)).execute();

furnace.removeByName("minecraft:gold_ingot_from_smelting_nether_gold_ore");
blastFurnace.removeByName("minecraft:gold_ingot_from_blasting_nether_gold_ore");

function provideBygOreSmeltingCompat(id as string, name as string, xp as float) as void {
    blastFurnace.remove(<item:byg:${id}>);
    furnace.addRecipe("smelting_" + name + "_ore", <item:byg:${id}>, <tagmanager:items>.tag(<resource:forge:ores/${name}>), xp, 200);
    blastFurnace.addRecipe("blasting_" + name + "_ore", <item:byg:${id}>, <tagmanager:items>.tag(<resource:forge:ores/${name}>), xp, 100);
}

provideBygOreSmeltingCompat("pendorite_scraps", "pendorite", 2.0);
provideBygOreSmeltingCompat("emeraldite_shards", "emeraldite", 1.0);
provideBygOreSmeltingCompat("lignite", "lignite", 0.1);
provideBygOreSmeltingCompat("anthracite", "anthracite", 0.1);
provideBygOreSmeltingCompat("ametrine_gems", "ametrine", 0.5);

furnace.removeByRegex("byg:.*_from_smelting_.*_ore");
blastFurnace.removeByRegex("byg:.*_from_blasting_.*_ore");

craftingTable.removeByRegex("excavated_variants:.*_conversion");

//proper byg pendorite smelting
furnace.addRecipe("smelting_raw_pendorite", pendorite_scraps, raw_pendorite, 2.0, 200);
blastFurnace.addRecipe("blasting_raw_pendorite", pendorite_scraps, raw_pendorite, 2.0, 100);

//fixing peat recipe
craftingTable.removeByName("byg:peat_from_crafting");
craftingTable.addShaped("peat", peat * 2, 
[[mud_wb, coals],
[coals, mud_wb]]);

//more stone types compat
Replacer.forOutput(<item:minecraft:stonecutter>, craftingTable).replace(stone, stone_tag).execute();
Replacer.forOutput(<item:storagedrawers:controller_slave>, craftingTable).replace(stone, stone_tag).execute();
Replacer.forOutput(<item:minecraft:repeater>, craftingTable).replace(stone, stone_tag).execute();
Replacer.forOutput(<item:quark:redstone_randomizer>, craftingTable).replace(stone, stone_tag).execute();
Replacer.forOutput(<item:minecraft:comparator>, craftingTable).replace(stone, stone_tag).execute();
Replacer.forOutput(<item:storagedrawers:compacting_drawers_3>, craftingTable).replace(stone, stone_tag).execute();
Replacer.forOutput(<item:storagedrawers:controller>, craftingTable).replace(stone, stone_tag).execute();

//smooth endstone automation
var smooth_endstone = <item:geode:smooth_endstone>;
furnace.addRecipe("smooth_endstone", smooth_endstone, end_stone, 0.1, 200);

//stone types automation
craftingTable.remove(dacite);
craftingTable.addShaped("dacite", dacite * 2, 
[[andesite, quartz],
[quartz, andesite]]);
craftingTable.remove(red_rock);
craftingTable.addShapeless("red_rock", red_rock * 2, [red_sand, cobblestone]);
Replacer.forOutput(soapstone, craftingTable).replace(stone_crafting_materials, cobblestone).execute();
Replacer.forOutput(travertine, craftingTable).replace(stone_crafting_materials, cobblestone).execute();
craftingTable.addShapeless("deepslate", deepslate * 2, [flint_block, stone]);
craftingTable.addShaped("shale", shale * 2,
[[stone, flint],
[flint, stone]]);
craftingTable.addShapeless("calcite", calcite, [diorite, clay_ball]);
craftingTable.addShaped("jasper", jasper * 2,
[[stone, quartz],
[quartz, stone]]);
craftingTable.addShapeless("limestone", limestone, [calcite, clay_ball]);
craftingTable.addShapeless("permafrost", permafrost * 2, [stone, packed_ice]);
craftingTable.addShapeless("myalite", myalite, [purpur_stone, clay_ball]);
craftingTable.addShapeless("ether_stone", ether_stone * 2, [myalite, deepslate]);

//clayworks vertical slabs recipes like other ones
craftingTable.remove(<item:clayworks:terracotta_vertical_slab>);
craftingTable.addShaped("terracotta_vertical_slab", <item:clayworks:terracotta_vertical_slab> * 3, [[<item:clayworks:terracotta_slab>], [<item:clayworks:terracotta_slab>], [<item:clayworks:terracotta_slab>]]);
craftingTable.remove(<item:clayworks:terracotta_brick_vertical_slab>);
craftingTable.addShaped("terracotta_brick_vertical_slab", <item:clayworks:terracotta_brick_vertical_slab> * 3, [[<item:clayworks:terracotta_brick_slab>], [<item:clayworks:terracotta_brick_slab>], [<item:clayworks:terracotta_brick_slab>]]);

for color in colors {
	craftingTable.remove(<item:clayworks:${color}_terracotta_vertical_slab>);
	craftingTable.addShaped(color + "_terracotta_vertical_slab", <item:clayworks:${color}_terracotta_vertical_slab> * 3, [[<item:clayworks:${color}_terracotta_slab>], [<item:clayworks:${color}_terracotta_slab>], [<item:clayworks:${color}_terracotta_slab>]]);
	craftingTable.remove(<item:clayworks:${color}_terracotta_brick_vertical_slab>);
	craftingTable.addShaped(color + "_terracotta_brick_vertical_slab", <item:clayworks:${color}_terracotta_brick_vertical_slab> * 3, [[<item:clayworks:${color}_terracotta_brick_slab>], [<item:clayworks:${color}_terracotta_brick_slab>], [<item:clayworks:${color}_terracotta_brick_slab>]]);
}
